# Modules finding ...
!load( message_logging ) : error( Can not find feature \"message_logging\" )
!load( module_finding ) : errorLogging( Can not find feature \"module_finding\" )

defineTest( loadDependsOnly ) \
{
    unset(DEPENDS.progress)

    for( module, ARGS ) \
    {
        contains( DEPENDS.ordered, "$${module}" ) \
        {
            DEPENDS.ordered -= "$${module}"
        } \

        DEPENDS.ordered += "$${module}"

        for( subdir, DEPEND_PATH ) \
        {
            project_entry = "$${subdir}/$${module}.prf"
            exists( "$${project_entry}" ): \
                break()
        }

        exists( "$${project_entry}" ) \
        {
            unset(DEPENDS)
            include( "$${project_entry}" )

            for( depend, DEPENDS ) \
            {
                contains( DEPENDS.ordered, "$${depend}" ) \
                {
                    # Add to tail (the static libraries order)
                    DEPENDS.ordered -= "$${depend}"
                    DEPENDS.ordered += "$${depend}"
                }

                contains( DEPENDS.progress, "$${depend}" ) \
                {
                    DEPENDS.progress -= $${DEPENDS}
                }
                DEPENDS.progress += $${DEPENDS}
            }
        } \
        else \
        {
            MODULE_PRF =
            !isEqual( TARGET, "$${module}" ) : \
                warningLogging( Can not resolve dependency $${module} )
        }
    }

    export( DEPENDS.progress )
    export( DEPENDS.ordered )
    return( true )
}

defineTest( loadDependencies ) \
{
    for( module, ARGS ) \
    {
        for( subdir, DEPEND_PATH ) \
        {
            project_entry = "$${subdir}/$${module}.prf"
            exists( "$${project_entry}" ): \
                break()
        }

        exists( "$${project_entry}" ) \
        {
            messageLogging( Loading the dependency $${module} from \"$${project_entry}\" )
            include( "$${project_entry}" )
        } \
        else \
        {
            MODULE_PRF =
            !isEqual( TARGET, "$${module}" ) : \
                warningLogging( Can not resolve dependency $${module} )
        }
    }

    # This list can be expanded if needed (in 2 places in this file)
    export( CONFIG )
    export( QMAKE_CXXFLAGS )
    export( INCLUDEPATH )
    export( DEPENDPATH )
    export( LIBS )
    export( QT )
    export( DEFINES )
    export( HEADERS )
    export( SOURCES )
    export( FORMS )
    export( LEXSOURCES )
    export( RES_FILE )
    export( RESOURCES )
    export( TRANSLATIONS )
    export( DISTFILES )
    return( true )
}

defineTest( resolveDependencies ) \
{
    # Include own dependencies (with special endian)
    BASE_NAME = "$$basename( _PRO_FILE_ )</>"
    BASE_NAME = $$replace( BASE_NAME, "pro</>", "prf" )
    MODULE_PRF = "$${_PRO_FILE_PWD_}/$${BASE_NAME}"

    exists( "$${MODULE_PRF}" ) \
    {
        include( "$${MODULE_PRF}" )
    } \

    messageLogging( Dependencies resolving ... )

    DEPENDS.progress = $${DEPENDS}

    for( i, 0..1000 ) \
    {
        !isEmpty( DEPENDS.progress ) \
        {
            loadDependsOnly( $${DEPENDS.progress} )
        } \
        else \
        {
            break()
        }
    }

    # more than
    !isEmpty( DEPENDS.progress ) \
    {
        error(Fatal dependency resolving)
    }

    loadDependencies($${DEPENDS.ordered})
    DEPENDS = $${DEPENDS.ordered}

    # Delete self
    LIBS -= -l$${TARGET}

    # This list can be expanded if needed (in 2 places in this file)
    export( CONFIG )
    export( QMAKE_CXXFLAGS )
    export( INCLUDEPATH )
    export( DEPENDPATH )
    export( LIBS )
    export( QT )
    export( DEFINES )
    export( HEADERS )
    export( SOURCES )
    export( FORMS )
    export( LEXSOURCES )
    export( RES_FILE )
    export( RESOURCES )
    export( TRANSLATIONS )
    export( DISTFILES )
    export( DEPENDS )
    return( true )
}



# global
resolveDependencies()
