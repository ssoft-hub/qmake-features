# Если атрибут pwd переменой GIT_VERSION (GIT.pwd) не указан явно,
# то считается, что проектный файл является частью репозитория.
# Либо можно явно задать значение GIT.pwd, если проектный файл
# не является частью репозитория (например, для проектов 3rdparty).

!load( log_message ) : error( Can not find feature \"log_message\" )

isEmpty( GIT.pwd ) : \
    GIT.pwd = $${_PRO_FILE_PWD_}

GIT.version = $$system( cd \"$${GIT.pwd}\" && git describe )
GIT.branch = $$system( cd \"$${GIT.pwd}\" && git symbolic-ref --short HEAD )
GIT.revision = $$system( cd \"$${GIT.pwd}\" && git rev-parse --short HEAD )

DEFINES *= \
    GIT_VERSION=\\\"$${GIT.version}\\\" \
    GIT_BRANCH=\\\"$${GIT.branch}\\\" \
    GIT_REVISION=\\\"$${GIT.revision}\\\" \

!isEmpty( GIT.revision ) : \
    logMessage( Git version of $${TARGET} is $${GIT.version}.$${GIT.revision} branch: $${GIT.branch} )
