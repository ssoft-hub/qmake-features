# Если атрибут GIT.pwd не указан явно, то считается, что проектный файл
# является частью репозитория. Либо можно явно задать значение GIT.pwd,
# если проектный файл не является частью репозитория (например, для проектов 3rdparty).

!load( message_logging ) : error( Can not find feature \"message_logging\" )
!load( file_system ) : error( Can not find feature \"file_system\" )
!load( include_files ) : error( Can not find feature \"include_files\" )

isEmpty( GIT.pwd ) : \
    GIT.pwd = $$commonDirectoryForSources()

!isEmpty( GIT.pwd ) {
    GIT.version = $$system( cd \"$${GIT.pwd}\" && git describe )
    GIT.branch = $$system( cd \"$${GIT.pwd}\" && git symbolic-ref --short HEAD )
    GIT.revision = $$system( cd \"$${GIT.pwd}\" && git rev-parse --short HEAD )

    !isEmpty( GIT.revision ) {
        messageLogging( "Git version of $${TARGET} \($${GIT.pwd}\) is \"$${GIT.version}\", revision is \"$${GIT.revision}\", branch is \"$${GIT.branch}\"." )

        DEFINES *= \
            GIT_VERSION=\\\"$${GIT.version}\\\" \
            GIT_BRANCH=\\\"$${GIT.branch}\\\" \
            GIT_REVISION=\\\"$${GIT.revision}\\\" \
    }
}
