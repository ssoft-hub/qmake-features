# Если атрибут pwd переменой GIT_VERSION (GIT_VERSION.pwd) не указан явно,
# то считается, что проектный файл является частью репозитория.
# Либо можно явно задать значение GIT_VERSION.pwd, если проектный файл
# не является частью репозитория (например, для проектов 3rdparty).

isEmpty( GIT_VERSION.pwd ) : \
    GIT_VERSION.pwd = $${_PRO_FILE_PWD_}

GIT_VERSION_CD_COMMAND = "cd \"$${GIT_VERSION.pwd}\" &&"
GIT_VERSION.version = $$system( $${GIT_VERSION_CD_COMMAND} git describe )
GIT_VERSION.branch = $$system( $${GIT_VERSION_CD_COMMAND} git symbolic-ref --short HEAD )
GIT_VERSION.revision = $$system( $${GIT_VERSION_CD_COMMAND} git rev-parse --short HEAD )

DEFINES *= GIT_VERSION=\\\"$${GIT_VERSION.version}\\\"
DEFINES *= GIT_BRANCH=\\\"$${GIT_VERSION.branch}\\\"
DEFINES *= GIT_REVISION=\\\"$${GIT_VERSION.revision}\\\"

!isEmpty( GIT_VERSION.revision ) : \
    message( [$$basename( _PRO_FILE_ )]: Git version of $${TARGET} is $${GIT_VERSION.version}.$${GIT_VERSION.revision} branch: $${GIT_VERSION.branch} )

unset( GIT_VERSION_CD_COMMAND )
