!load( file_system ) : error( Can not find feature \"file_system\" )
!load( message_logging ) : error( Can not find feature \"message_logging\" )

# Сооставлят список поддиректорий до 10 уровней вложености
################################################################################

defineTest( addSubdirs ) {
    result = false

    for( subdir, ARGS ) {
        entries = $$files( $${subdir}/* )
        for( entry, entries ) {

            sub_entries = $$files( $${entry}/* )

            !isEmpty( sub_entries ) {
                !contains( __RESOLVED_DIRS__, $${entry} ) {
                    __RESOLVED_DIRS__ *= $${entry}
                    result = true
                }
            }
        }
    }
    export( __RESOLVED_DIRS__ )
    return( $${result} )
}

defineTest( addSubdirsRecursive ) {
    for( tmp, 1..10 ) {
        !addSubdirs( $${__RESOLVED_DIRS__} ) : break()
    }
    export ( __RESOLVED_DIRS__ )
    return ( true )
}

# Включает все исходные файлы из заданных дирректорий
################################################################################

defineTest( includeFilesFrom ) {

    dir = $${ARGS}
    entries = $$files( $${dir}/* )

    isEmpty( entries ) {
        return ( false )
    }

    HEADERS *= $$files( $${dir}/*.h )
    HEADERS *= $$files( $${dir}/*.hpp )
    SOURCES *= $$files( $${dir}/*.c )
    SOURCES *= $$files( $${dir}/*.cpp )
    FORMS *= $$files( $${dir}/*.ui )
    LEXSOURCES *= $$files( $${dir}/*.l )
    RES_FILE *= $$files( $${dir}/*.rc )
    RESOURCES *= $$files( $${dir}/*.qrc )
    TRANSLATIONS *= $$files( $${dir}/*.ts )
    DISTFILES *= $$files( $${dir}/*.vert )
    DISTFILES *= $$files( $${dir}/*.frag )

    export( HEADERS )
    export( SOURCES )
    export( FORMS )
    export( LEXSOURCES )
    export( RES_FILE )
    export( RESOURCES )
    export( TRANSLATIONS )
    export( DISTFILES )

    return( true )
}


# Включает все исходные файлы из заданных дирректорий и их поддирректорий
################################################################################

defineTest( includeFiles ) {

    __RESOLVED_DIRS__ = $${ARGS}

    addSubdirsRecursive()

    for( dir, __RESOLVED_DIRS__ ) {
        includeFilesFrom( $${dir} )
    }

    export( HEADERS )
    export( SOURCES )
    export( FORMS )
    export( LEXSOURCES )
    export( RES_FILE )
    export( RESOURCES )
    export( TRANSLATIONS )
    export( DISTFILES )

    return( true )
}

defineReplace( privateJoinCommonDirectory ) {
    common = "$$1"
    dir = "$$2"

    isEmpty( common ) {
        common = "$${dir}"
        return( $${common} )
    }

    !isEmpty( dir ) {
        common = "$$commonPathForTwoPaths( "$${common}", "$${dir}" )"
    }
    return( "$${common}" )
}


# Находит общую базовую директорию для всех файлов исходников
defineReplace( commonDirectoryForSources ) {
    common =
    dir = $$commonPathForPaths( $${HEADERS} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${SOURCES} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${LEXSOURCES} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${RES_FILE} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${RESOURCES} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${TRANSLATIONS} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"
    dir = $$commonPathForPaths( $${DISTFILES} )
    common = "$$privateJoinCommonDirectory( "$${common}", "$${dir}" )"

    return( "$${common}" )
}
