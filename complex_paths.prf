# Пути сборки комплекса
################################################################################

# Поиск общей конфигурации
!load( qmake_conf_path ) : error( Can not find feature \"qmake_conf_path\" )

# Исправление багов qmake Qt4.
isEqual( QT_MAJOR_VERSION, 4 ) {
    TOOL_PATH = qt.$${QT_VERSION}
} else {
    TOOL_PATH = qt.$${QT_VERSION}-$$basename(QMAKESPEC)
}

CONFIG( debug, debug|release ) {
    DESTDIR = $${QMAKE_CONF_FILE_PWD}/bin/$${TOOL_PATH}/debug
    GENERATED_FILES_PWD=$${QMAKE_CONF_FILE_PWD}/build/$${TOOL_PATH}/debug
} else {
    DESTDIR = $${QMAKE_CONF_FILE_PWD}/bin/$${TOOL_PATH}/release
    GENERATED_FILES_PWD=$${QMAKE_CONF_FILE_PWD}/build/$${TOOL_PATH}/release
}

GENERATED_FILES_DIR=$${GENERATED_FILES_PWD}/$${TARGET}

LIBS *= -L$${DESTDIR}
OBJECTS_DIR = $${GENERATED_FILES_DIR}/obj
# Qt4: Не находит mocinclude.tmp в нужном месте
#MOC_DIR = $${GENERATED_FILES_DIR}/moc
RCC_DIR = $${GENERATED_FILES_DIR}/rcc
UI_DIR = $${GENERATED_FILES_DIR}/ui
